apply plugin: 'base'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'war'
apply from: 'https://raw.github.com/akhikhl/gretty/master/pluginScripts/gretty.plugin'

group = 'com.dzlier-rptools'
version = '1.0.0-SNAPSHOT'

description = """RPTools"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

clean.delete << file('node_modules')
clean.delete << file('out')
clean.delete << file('src/main/webapp/resources/js/bower')

buildscript {
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
//        maven { url 'http://dl.bintray.com/vermeulen-mp/gradle-plugins' }
    }

    dependencies {
        classpath 'com.moowork.gradle:gradle-node-plugin:0.13'
//        classpath 'com.wiredforcode:gradle-heroku-plugin:1.0.0'
    }
}

apply plugin: 'com.moowork.node'
//apply plugin: 'heroku'

repositories {
    jcenter()
    mavenCentral()
    maven { url "http://repo.maven.apache.org/maven2" }
}
dependencies {
    compile project(':dzlier-collections')
    compile group: 'commons-io', name: 'commons-io', version: '2.2'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.3.1'
    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    compile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'
    compile group: 'net.java.openjfx.backport', name: 'openjfx-78-backport', version: '1.8.0-ea-b96.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    compile group: 'org.apache.commons', name: 'commons-math3', version: '3.5'
    compile group: 'org.apache.velocity', name: 'velocity', version: '1.7'
    compile group: 'org.aspectj', name: 'aspectjweaver', version: '1.8.8'
    compile group: 'org.eclipse.jetty', name: 'jetty-runner', version: '9.3.3.v20150827'
    compile group: 'org.projectlombok', name: 'lombok', version: '1.16.6'
    compile group: 'org.springframework', name: 'spring-context-support', version: '4.2.4.RELEASE'
    compile group: 'org.springframework', name: 'spring-webmvc', version: '4.2.4.RELEASE'
}

gretty.contextPath = '/'

//heroku {
//    //get this from heroku
//    apiKey = '323f842e-9907-4f70-ad35-0edadcb28b2d'
//
//    //set this on first run if you don't want a generated name
//    appName = 'rptools-2'
//}

node {
    version = '0.12.2'
    npmVersion = '2.7.5'
    download = true
}

task npmCacheConfig(type: NpmTask) {
    description = "Configure the NPM cache"
    def npmCacheDir = "${gradle.getGradleUserHomeDir()}/caches/npm"
    outputs.files file(npmCacheDir)
    args = ['config', 'set', 'cache', npmCacheDir]
}

task npmPackages(type: NpmTask, dependsOn: npmCacheConfig) {
    description = "Install Node.js packages"
    args = ['install']
    inputs.files file('package.json')
    outputs.files file('node_modules')
}

task bowerInstall(type: NodeTask) {
    script = file('node_modules/bower/bin/bower')
    args = ["--config.storage.cache=${gradle.getGradleUserHomeDir()}/caches/bower/cache",
            "--config.storage.packages=${gradle.getGradleUserHomeDir()}/caches/bower/packages",
            "--config.storage.registry=${gradle.getGradleUserHomeDir()}/caches/bower/registry",
            'install']
    inputs.files file('bower.json')
    outputs.files file('src/main/webapp/resources/js/bower')
    dependsOn npmPackages
}

task bowerPackages(type: Sync) {
    from 'bower_components'
    into "src/main/webapp/resources/js/bower"
    exclude '**/test/*'
    exclude '**/demo/*'
    include '**/*.html'
    dependsOn bowerInstall
}

processResources.dependsOn bowerPackages

project.afterEvaluate {
    project.tasks.appRun.dependsOn bowerPackages
}

task stage(dependsOn: ['clean', 'appRun'])