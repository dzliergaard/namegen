apply plugin: 'java'
apply plugin: 'war'

group = 'com.rptools'
version = '1.0'

description = """RPTools"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

clean.delete << file('bower_components')
clean.delete << file('node_modules')
clean.delete << file('out')
clean.delete << file('src/main/webapp/resources/js/bower')

buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.0'
        classpath 'com.moowork.gradle:gradle-node-plugin:0.13'
//        classpath 'com.android.tools.build:gradle:1.3.1'
    }
}

apply plugin: 'com.moowork.node'
apply plugin: 'com.bmuschko.tomcat'

repositories {
    mavenCentral()
    mavenLocal()
}
dependencies {
    compile project(':rptools')
    compile group: 'com.github.jsimone', name: 'webapp-runner', version: '8.0.30.2'
    compile group: 'org.apache.velocity', name: 'velocity', version: '1.7'
    compile group: 'org.aspectj', name: 'aspectjweaver', version: '1.8.8'
    compile group: 'org.springframework', name: 'spring-webmvc', version: '4.2.4.RELEASE'
    providedCompile group: 'javax.servlet', name: 'javax.servlet-api', version: '3.1.0'

    def tomcatVersion = '8.0.27'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-logging-juli:${tomcatVersion}"
    tomcat("org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}") {
        exclude group: 'org.eclipse.jdt.core.compiler', module: 'ecj'
    }
}

tomcatRun.contextPath = '/'
tomcatRunWar.contextPath = '/'

node {
    version = '0.12.2'
    npmVersion = '2.7.5'
    download = true
}

task npmCacheConfig(type: NpmTask, dependsOn: npmInstall) {
    description = "Configure the NPM cache"
    def npmCacheDir = "${gradle.getGradleUserHomeDir()}/caches/npm"
    outputs.files file(npmCacheDir)
    args = ['config', 'set', 'cache', npmCacheDir]
}

task npmPackages(type: NpmTask, dependsOn: npmCacheConfig) {
    description = "Install Node.js packages"
    args = ['install']
    inputs.files file('package.json')
    outputs.files file('node_modules')
}

task bowerInstall(type: NodeTask) {
    script = file('node_modules/bower/bin/bower')
    args = ['install']
    inputs.files file('bower.json')
    dependsOn npmPackages
}

task bowerPackages(type: Sync) {
    from 'bower_components'
    into "src/main/webapp/resources/js/bower"
    exclude '**/test'
    exclude '**/demo'
    include '**/*.html'
    include '**/*.js'
    include '**/*.css'
    dependsOn bowerInstall
    mustRunAfter clean
}

processResources.dependsOn bowerPackages

task stage() {
    dependsOn clean, bowerPackages, war
}
tasks.stage.doLast() {
    delete fileTree(dir: "build/distributions")
    delete fileTree(dir: "build/assetCompile")
    delete fileTree(dir: "build/distributions")
    delete fileTree(dir: "build/libs", exclude: "*.war")
}

war.mustRunAfter clean

task copyToLib(type: Copy) {
    into "$buildDir/server"
    from(configurations.compile) {
        include "webapp-runner*"
    }
}

stage.dependsOn(copyToLib)
